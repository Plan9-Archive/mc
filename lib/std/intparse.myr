use "chartype"
use "die"
use "hasprefix"
use "option"
use "types"
use "utf"

pkg std =
	generic intparsebase	: (s : byte[:], base : int -> option(@a::(integral,numeric)))
	generic intparse	: (s : byte[:]	-> option(@a::(integral,numeric)))
;;

generic intparse = {s
	var isneg 

	isneg = false
	if hasprefix(s, "-")
		s = s[1:]
		isneg = true
	;;

	if hasprefix(s, "0x")
		-> doparse(s[2:], isneg, 16)
	elif hasprefix(s, "0o")
		-> doparse(s[2:], isneg, 8)
	elif hasprefix(s, "0b")
		-> doparse(s[2:], isneg, 2)
	else
		-> doparse(s, isneg, 10)
	;;
}

generic intparsebase = {s, base
	var isneg 

	isneg = false
	if hasprefix(s, "-")
		s = s[1:]
		isneg = true
	;;

	-> doparse(s, isneg, base)
}

generic doparse = {s, isneg, base
	var v
	var cv : int32
	
	v = 0
	for c in std.bychar(s)
		if c == '_'
			continue
		;;
		cv = charval(c, base)
		if cv >= 0
			v *= (base castto(@a::(integral,numeric)))
			v += cv castto(@a::(integral,numeric))
		else
			-> `None
		;;
	;;

	if isneg
		-> `Some -v
	else
		-> `Some v
	;;
}
